plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.15'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'jacoco'
}

group = 'com.signify'
version = '1.0'

java {
	sourceCompatibility = '1.8'
}

springBoot {
	mainClass = 'com.signify.reviews.ReviewsApplication'
}

jacoco {
	toolVersion = "0.8.9"
	reportsDirectory = file("$buildDir/jacoco")
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = false
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
	afterEvaluate { // (optional) : to exclude classes / packages from coverage
		classDirectories.from = files(classDirectories.files.collect {
			fileTree(
					dir: it,
					exclude: [ 'com/signify/reviews/dto/**',
							   'com/signify/reviews/dto/response/**',
							   'com/signify/reviews/dao/model/**',
							   'com/signify/reviews/dao/filter/**'
					])
		})
	}
}


/*
jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
	reports {
		xml.enabled false
		csv.enabled false
		html.enabled true
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
}
*/


repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.9'

	//testImplementation 'junit:junit:4.13.1'
	runtimeOnly 'com.h2database:h2'
	compileOnly 'org.projectlombok:lombok:1.18.28'
	annotationProcessor 'org.projectlombok:lombok:1.18.28'
	implementation group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version: '1.9.13'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// If using JUnit Jupiter
	testImplementation("org.junit.jupiter:junit-jupiter:5.9.2")
	testRuntimeOnly("org.junit.platform:junit-platform-launcher")

	testCompileOnly 'org.projectlombok:lombok:1.18.28'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport // report is always generated after tests run
	afterSuite { desc, result ->
		if (!desc.parent)
			println("${result.resultType} " +
					"(${result.testCount} tests, " +
					"${result.successfulTestCount} successes, " +
					"${result.failedTestCount} failures, " +
					"${result.skippedTestCount} skipped)")
	}
}


/*tasks.named('test') {
	useJUnitPlatform()
}*/
